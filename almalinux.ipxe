#!ipxe
set ipparam BOOTIF=${netX/mac} ip=dhcp ksdevice=bootif
set megaraid_enabled false

goto ${menu} ||

:almalinux
clear osversion
set os AlmaLinux
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch x86_64 ||
iseq ${os_arch} arm64 && set os_arch aarch64 ||
menu ${os} - ${os_arch}
item 9 ${space} ${os} 9 (Latest)
item 8 ${space} ${os} 8 (Latest)
isset ${osversion} || choose osversion || goto almalinux_exit
echo ${cls}

set kickstart http://${boot_domain}/netboot/kickstart/almalinux-${osversion}-${os_arch}.cfg
set dir ${osversion}/BaseOS/${os_arch}/os
set repo ${almalinux_mirror}/${dir}
goto boottype

:boottype
set ova ${os} ${osversion}
menu ${os} ${os_arch} boot type
item install ${ova} install
item
item megaraid_menu Use MegaRAID DUD [ ${megaraid_enabled} ]
item
item --gap Settings:
item
item --gap ks: ${kickstart}
item --gap repo: ${repo}
item --gap cmdline: ${cmdline}
isset ${bt} || choose bt || goto almalinux
iseq ${bt} install && goto bootos_images ||
iseq ${bt} megaraid_menu && goto megaraid_menu ||
goto bootos_images

:megaraid_menu
iseq ${osversion} 8 && set megaraid inst.dd=${elrepo_mirror}/dud/el8/x86_64/dd-megaraid_sas-07.725.01.00-1.el8_9.elrepo.iso ||
iseq ${osversion} 9 && set megaraid inst.dd=${elrepo_mirror}/dud/el9/x86_64/dd-megaraid_sas-07.725.01.00-1.el9_3.elrepo.iso ||
set megaraid_enabled true
clear bt
goto almalinux

:bootos_images
imgfree
kernel ${repo}/images/pxeboot/vmlinuz network inst.repo=${repo}/ inst.text inst.gpt ${megaraid} inst.ks=${kickstart} ${ipparam} initrd=initrd.img ${cmdline}
initrd ${repo}/images/pxeboot/initrd.img
echo MD5sums:
md5sum vmlinuz initrd.img
boot
goto almalinux_exit

:almalinux_exit
clear menu
exit 0
