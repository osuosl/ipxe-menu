#!ipxe
set ipparam BOOTIF=${netX/mac} ip=dhcp ksdevice=bootif

goto ${menu} ||

:almalinux
clear osversion
set os AlmaLinux
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch x86_64 ||
iseq ${os_arch} arm64 && set os_arch aarch64 ||
menu ${os} - ${os_arch}
item 10.0 ${space} ${os} 10.0 (Latest)
item 9 ${space} ${os} 9 (Latest)
item 8 ${space} ${os} 8 (Latest)
isset ${osversion} || choose osversion || goto almalinux_exit
echo ${cls}

set kickstart http://${boot_domain}/netboot/kickstart/almalinux-${osversion}-${os_arch}.cfg
set dir ${osversion}/BaseOS/${os_arch}/os
set repo ${almalinux_mirror}/${dir}
iseq ${osversion} 10.0 && set repo http://vault.almalinux.org/${dir} || true
iseq ${osversion} 10.0 && set alma10params inst.rdp inst.rdp.username=osuadmin inst.rdp.password=osuadmin || set alma10params
goto boottype

:boottype
set ova ${os} ${osversion}
menu ${os} ${os_arch} boot type
item install ${ova} install
item
item --gap Settings:
item
item --gap ks: ${kickstart}
item --gap repo: ${repo}
item --gap cmdline: ${cmdline}
isset ${bt} || choose bt || goto almalinux
iseq ${bt} install && goto bootos_images ||
goto bootos_images

:bootos_images
imgfree
kernel ${repo}/images/pxeboot/vmlinuz network inst.repo=${repo}/ inst.text inst.gpt inst.ks=${kickstart} ${ipparam} ${alma10params} initrd=initrd.img ${cmdline}
initrd ${repo}/images/pxeboot/initrd.img
echo MD5sums:
md5sum vmlinuz initrd.img
boot
goto almalinux_exit

:almalinux_exit
clear menu
exit 0
