#!ipxe

:start
chain --autofree boot.cfg ||
iseq ${cls} serial && goto ignore_cls ||
set cls:hex 1b:5b:4a  # ANSI clear screen sequence - "^[[J"
set cls ${cls:string}
:ignore_cls

isset ${arch} && goto skip_arch_detect ||
cpuid --ext 29 && set arch x86_64 || set arch i386
:skip_arch_detect
isset ${menu} && goto ${menu} ||

isset ${ip} || dhcp || echo DHCP failed

:main_menu
clear menu
set space:hex 20:20
set space ${space:string}
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
menu ${site_name}
item --gap Default:
item razor ${space} Boot from Razor with tftp://${razor_server}/${razor_file}
item local ${space} Boot from local hdd
item --gap Tools:
item shell ${space} iPXE shell
item netinfo ${space} Network card info
item netboot_xyz ${space} Boot from netboot.xyz
item reboot ${space} Reboot machine
isset ${menu} && set timeout 0 || set timeout 5000
choose --timeout ${timeout} --default ${menu} menu || goto razor
echo ${cls}
goto ${menu} ||

:change_menu
chain ${menu}.ipxe || goto error
goto main_menu

:error
echo Error occured, press any key to return to menu ...
prompt
goto main_menu

:razor
echo Booting from Razor via ${primary_network} with tftp://${razor_server}/${razor_file}
chain tftp://${razor_server}/${razor_file} || echo Error loading Razor, trying local drive && goto local

:netboot_xyz
echo Booting from boot.netboot.xyz
chain --autofree https://boot.netboot.xyz || goto main_menu

:local
echo Booting from local disks ...
exit 0

:shell
echo Type "exit" to return to menu.
set menu main_menu
shell
goto main_menu

:reboot
echo Rebooting machine
reboot
