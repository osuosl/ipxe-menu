#!ipxe
set firmware_enabled false

goto ${menu} ||

:debian
set os Debian
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch amd64 ||
iseq ${os_arch} i386 && set os_arch i386 ||
iseq ${os_arch} arm64 && set os_arch arm64 ||
menu ${os} - ${os_arch}
item bookworm ${space} ${os} 12.0 (bookworm)
choose debian_version || goto debian_exit
goto mirrorcfg

:mirrorcfg
set debian_mirror ${debian_mirror}
set mirrorcfg mirror/suite=${debian_version}
set dir ${debian_base_dir}/dists/${debian_version}/main/installer-${os_arch}/current/images/netboot/${menu}-installer/${os_arch}
set preseedurl http://${boot_domain}/netboot/preseed/debian.cfg
set locales debian-installer=en_US locale=en_US kbd-chooser/method=us console-setup/ask_detect=false
set install_params install auto=true priority=critical ${locales} preseed/url=${preseedurl}
iseq ${firmware_enabled} false && set initrd_url ${debian_mirror}/${dir}/initrd.gz ||
goto boottype

:boottype
set ova ${os} ${debian_version}
menu ${os} ${os_arch} boot type
item install ${ova} install
item
item firmware_menu Use Non-free Firmware [ ${firmware_enabled} ]
item
item --gap Settings:
item
item --gap preseed: ${preseedurl}
item --gap repo: ${debian_mirror}
item --gap cmdline: ${cmdline}
item --gap initrd_url: ${initrd_url}
isset ${bt} || choose bt || goto debian
iseq ${bt} install && goto deb_boot ||
iseq ${bt} firmware_menu && goto firmware_menu ||
goto deb_boot

:firmware_menu
set initrd_url http://${boot_domain}/http/debian/${debian_version}/initrd.gz
set firmware_enabled true
clear bt
goto debian

:deb_boot
imgfree
kernel ${debian_mirror}/${dir}/linux ${install_params} interface=auto ${mirrorcfg} initrd=initrd.gz ${cmdline}
initrd ${initrd_url}
echo
echo MD5sums:
md5sum linux initrd.gz
boot

:debian_exit
clear menu
exit 0
